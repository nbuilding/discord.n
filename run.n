import FileIO
import json
import times

let random = imp "./.testmods/random.n"

let token = imp "./token.n"

let discord = imp "./discord.n"

let responses = {
	..discord.responsesEmpty,
	onReady: yes((self:discord.member.Member) -> cmd[()] {
		print("Bot is up!")
	}),
	onMessage: yes((message:discord.message.Message) -> cmd[bool] {
		if message.getSender().getId() == "799715319127736392" {
			return false
		}

		print(message.getContent())

		if message.getContent() == "ntest" {
			let response = discord.sendMessage(message.getChannel(), yes("testing"), none, none, none, none)!
			if response ~= 200 {
				print("unable to send message")
			}
		}
		if message.getContent() == "nembed" {
			let rand = random.createRandom((times.getTime()! * 1000.0).round())
			let response = discord.sendMessage(message.getChannel(), none, none, yes([
				discord.embed.Embed(
					yes("Testing"),
					yes("This is a test"),
					yes("https://ash.vin"),
					yes(discord.color.Color(
						(rand.getNext() * 256.0).floor(),
						(rand.getNext() * 256.0).floor(),
						(rand.getNext() * 256.0).floor(),
					)),
				)
			]), none, none)!
			if response ~= 200 {
				print("unable to send embed")
			}
		}
		if message.getContent() == "nexit" && message.getSender().getId() == "384499090865782785" {
			print("exiting...")
			return true
		} 

		return false
	}),
}

let presence = discord.status.Status(
	none,
	{
		name: "the output logs",
		type: discord.status.activity.WATCHING,
	},
	"online",
	false
)

let self = () -> cmd[()] {
	let mut key = FileIO.read(".token")! | ""

	if key == "" {
		key = token.KEY
	}

	print(discord.start(key, presence, responses)!)
		
}

let pub main = self()
